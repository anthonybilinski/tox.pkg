# Maintainer: Anthony Bilinski <me@abilinski.com>

pkgname=%PACKAGE%
pkgver=%VERSION%
pkgrel=1
pkgdesc="The future of online communications"
arch=('i686' 'x86_64')
url="https://github.com/irungentoo/toxcore"
license=('GPL-3')
depends=('systemd' 'libconfig' 'opus' 'libvpx' 'libsodium')
makedepends=('libtool' 'autoconf' 'automake' 'yasm')
backup=('etc/tox-bootstrapd.conf')
conflicts=('toxcore' 'libtoxcore-ngc')
provides=('toxcore')
install=${pkgname}.install
source=("https://build.opensuse.org/source/home:qTox/${pkgname}/${pkgname}_${pkgver}.tar.bz2")
sha512sums=('%SHA_512%')

build() {
    cd ${pkgname}

    ./autogen.sh
    ./configure --prefix=/usr --enable-daemon --disable-tests --disable-silent-rules
    make
}

package() {
    cd ${pkgname}

    make DESTDIR=${pkgdir} install

    rm -f ${pkgdir}/usr/bin/DHT_bootstrap

    install -D -m 0644 other/bootstrap_daemon/tox-bootstrapd.conf     ${pkgdir}/etc/tox-bootstrapd.conf
    install -D -m 0644 other/bootstrap_daemon/tox-bootstrapd.service  ${pkgdir}/usr/lib/systemd/system/tox-bootstrapd.service
    install -D -m 0644 other/bootstrap_daemon/tox-bootstrapd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/tox-bootstrapd.conf
    install -D -m 0644 other/bootstrap_daemon/tox-bootstrapd.users    ${pkgdir}/usr/lib/sysusers.d/tox-bootstrapd.conf

    install -D -m 0644 README.md ${pkgdir}/usr/share/doc/${pkgname}/README.md
    install -D -m 0644 COPYING   ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
